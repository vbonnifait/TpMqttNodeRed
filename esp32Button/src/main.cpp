/*********
  Rui Santos
  Complete project details at https://randomnerdtutorials.com
  Modified by Alexis Combes
*********/

#include <WiFi.h>
#include <PubSubClient.h>
#include <Wire.h>

// TODO : Replace the next variables with your SSID/Password combination
const char *ssid = "xxxx";
const char *password = "xxxx";

// TODO : Add your MQTT Broker IP address, example:
// const char* mqtt_server = "192.168.1.144";
const char *mqtt_server = "XXX.XXX.XXX.XXX";

WiFiClient espClient;
PubSubClient client(espClient);
long lastMsg = 0;
char msg[50];
int value = 0;

// BUTTON Pin
const int buttonPin = 25;

void setup_wifi();
void callback(char *topic, byte *message, unsigned int length);

void setup()
{
  Serial.begin(115200);
  setup_wifi();
  client.setServer(mqtt_server, 1883);
  client.setCallback(callback);

  pinMode(buttonPin, INPUT_PULLUP);
}

void setup_wifi()
{
  delay(10);
  // We start by connecting to a WiFi network
  Serial.println();
  Serial.print("Connecting to ");
  Serial.println(ssid);

  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED)
  {
    delay(500);
    Serial.print(".");
  }

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
}

void callback(char *topic, byte *message, unsigned int length)
{
  Serial.print("Message arrived on topic: ");
  Serial.print(topic);
  Serial.print(". Message: ");
  String messageTemp;

  for (int i = 0; i < length; i++)
  {
    Serial.print((char)message[i]);
    messageTemp += (char)message[i];
  }
  Serial.println();
}

void reconnect()
{
  // Loop until we're reconnected
  while (!client.connected())
  {
    Serial.print("Attempting MQTT connection...");
    // Attempt to connect
    if (client.connect("ESP32Client_Btn"))
    {
      Serial.println("connected");
      // Subscribe
      client.subscribe("esp32/output");
    }
    else
    {
      Serial.print("failed, rc=");
      Serial.print(client.state());
      Serial.println(" try again in 5 seconds");
      // Wait 5 seconds before retrying
      delay(5000);
    }
  }
}
void loop()
{
  if (!client.connected())
  {
    reconnect();
  }
  client.loop();

  long now = millis();
  if (now - lastMsg > 1000)
  {
    lastMsg = now;

    // Read button state and publish it to MQTT
    int buttonState = digitalRead(buttonPin);
    if (buttonState == LOW)
    {
      Serial.print("Publish message: ");
      String msg = "down";
      Serial.println(msg);
      client.publish("esp32/button1", msg.c_str());
    }
    else
    {
      Serial.print("Publish message: ");
      String msg = "up";
      Serial.println(msg);
      client.publish("esp32/button1", msg.c_str());
    }
  }
}